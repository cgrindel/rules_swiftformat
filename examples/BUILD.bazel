load("@cgrindel_rules_bzlformat//bzlformat:bzlformat.bzl", "bzlformat_pkg")
load(
    "@cgrindel_rules_bazel_integration_test//bazel_integration_test:bazel_integration_test.bzl",
    "bazel_integration_test",
    "bazel_integration_tests",
    "integration_test_utils",
)
load("//:bazel_versions.bzl", "CURRENT_BAZEL_VERSION", "OTHER_BAZEL_VERSIONS")

bzlformat_pkg(name = "bzlformat")

# MARK: - Shared Attribute Values

ADDITIONAL_WORKSPACE_FILES = [
    # The example workspaces reference the *.bazelrc files in the parent.
    "//:shared_bazelrc_files",

    # The example workspaces use local_repository to reference this
    # workspace.  This target collects all of the files needed for
    # rules_spm to work in the child workspaces.
    "//:local_repository_files",
]

# MARK: - Integration Tests

_EXAMPLE_NAMES = [
    "simple",
    "exclude_files",
    "rules_swift_helpers",
]

[
    bazel_integration_test(
        name = example + "_test",
        bazel_version = CURRENT_BAZEL_VERSION,
        workspace_files = integration_test_utils.glob_workspace_files(example) +
                          ADDITIONAL_WORKSPACE_FILES,
    )
    for example in _EXAMPLE_NAMES
]

bazel_integration_tests(
    name = "simple_test",
    bazel_versions = OTHER_BAZEL_VERSIONS,
    workspace_files = integration_test_utils.glob_workspace_files("simple") +
                      ADDITIONAL_WORKSPACE_FILES,
)

# MARK: - Test Suites

test_suite(
    name = "all_integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = [
        example + "_test"
        for example in _EXAMPLE_NAMES
    ] + integration_test_utils.bazel_integration_test_names(
        "simple_test",
        OTHER_BAZEL_VERSIONS,
    ),
    visibility = ["//:__subpackages__"],
)

load("@cgrindel_bazel_starlib//bzlformat:defs.bzl", "bzlformat_pkg")
load(
    "@contrib_rules_bazel_integration_test//bazel_integration_test:defs.bzl",
    "bazel_integration_test",
    "bazel_integration_tests",
    "default_test_runner",
    "integration_test_utils",
)
load("//:bazel_versions.bzl", "CURRENT_BAZEL_VERSION", "OTHER_BAZEL_VERSIONS")

bzlformat_pkg(name = "bzlformat")

# MARK: - Shared Attribute Values

ADDITIONAL_WORKSPACE_FILES = [
    # The example workspaces reference the *.bazelrc files in the parent.
    "//:shared_bazelrc_files",

    # The example workspaces use local_repository to reference this
    # workspace.  This target collects all of the files needed for
    # rules_spm to work in the child workspaces.
    "//:local_repository_files",
]

# MARK: - Integration Tests

_EXAMPLE_NAMES = [
    "simple",
    "exclude_files",
    "rules_swift_helpers",
]

default_test_runner(
    name = "default_test_runner",
)

# Integration tests executed against current version of Bazel
[
    bazel_integration_test(
        name = example + "_test",
        bazel_version = CURRENT_BAZEL_VERSION,
        test_runner = ":default_test_runner",
        workspace_files = integration_test_utils.glob_workspace_files(example) +
                          ADDITIONAL_WORKSPACE_FILES,
        workspace_path = example,
    )
    for example in _EXAMPLE_NAMES
]

sh_binary(
    name = "change_update_all_test_runner",
    testonly = True,
    srcs = ["change_update_all_test.sh"],
    data = [
        "@contrib_rules_bazel_integration_test//tools:create_scratch_dir",
    ],
    deps = [
        "@bazel_tools//tools/bash/runfiles",
        "@cgrindel_bazel_starlib//shlib/lib:assertions",
    ],
)

# Integration tests executed against current version of Bazel with custom test runner
bazel_integration_test(
    name = "change_update_all_test",
    bazel_version = CURRENT_BAZEL_VERSION,
    test_runner = ":change_update_all_test_runner",
    workspace_files = integration_test_utils.glob_workspace_files("simple") +
                      ADDITIONAL_WORKSPACE_FILES,
    workspace_path = "simple",
)

bazel_integration_tests(
    name = "simple_test",
    bazel_versions = OTHER_BAZEL_VERSIONS,
    test_runner = ":default_test_runner",
    workspace_files = integration_test_utils.glob_workspace_files("simple") +
                      ADDITIONAL_WORKSPACE_FILES,
    workspace_path = "simple",
)

# MARK: - Test Suites

test_suite(
    name = "integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = [
        ":change_update_all_test",
    ] + [
        example + "_test"
        for example in _EXAMPLE_NAMES
    ] + integration_test_utils.bazel_integration_test_names(
        "simple_test",
        OTHER_BAZEL_VERSIONS,
    ),
    visibility = ["//:__subpackages__"],
)

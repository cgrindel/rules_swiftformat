load(
    "@cgrindel_bazel_starlib//bazeldoc:defs.bzl",
    "doc_for_provs",
    "write_file_list",
    "write_header",
    doc_providers = "providers",
)
load("@cgrindel_bazel_starlib//bzlformat:defs.bzl", "bzlformat_pkg")

# MARK: - Documentation Providers

_SWIFTTIDY_DOC_DEPS = [
    "//swiftformat:defs",
]

_SWIFTFORMAT_DOC_DEPS = [
    "//swiftformat:defs",
]

_SWIFTTIDY_RULES_DOC_PROVIDER = doc_providers.create(
    name = "rules_and_macros_overview",
    stardoc_input = "//swifttidy:defs.bzl",
    symbols = [
        "swiftformat_format",
        "swiftformat_pkg",
        "swiftformat_register_prebuilt_toolchains",
    ],
    deps = _SWIFTTIDY_DOC_DEPS,
)

_SWIFTFORMAT_RULES_DOC_PROVIDER = doc_providers.create(
    name = "swiftformat_rules_and_macros_overview",
    stardoc_input = "//swiftformat:defs.bzl",
    symbols = [
        "swiftformat_binary",
        "swiftformat_library",
        "swiftformat_test",
    ],
    deps = _SWIFTFORMAT_DOC_DEPS,
)

_API_SRCS = [
    "src_utils",
]

_API_DOC_PROVIDERS = [
    doc_providers.create(
        name = name,
        stardoc_input = "//swifttidy:defs.bzl",
        symbols = [name],
        deps = _SWIFTTIDY_DOC_DEPS,
    )
    for name in _API_SRCS
]

_ALL_DOC_PROVIDERS = [
    _SWIFTTIDY_RULES_DOC_PROVIDER,
    _SWIFTFORMAT_RULES_DOC_PROVIDER,
    doc_providers.create(
        name = "api",
        is_stardoc = False,
        stardoc_input = "//swifttidy:defs.bzl",
        deps = _SWIFTTIDY_DOC_DEPS,
    ),
] + _API_DOC_PROVIDERS

# MARK: - Headers

write_header(
    name = _SWIFTTIDY_RULES_DOC_PROVIDER.header_label,
    header_content = [
        "# Rules and Macros",
        "",
        "The rules and macros described below are used to tidy Swift source ",
        "files.",
    ],
    symbols = _SWIFTTIDY_RULES_DOC_PROVIDER.symbols,
)

write_header(
    name = _SWIFTFORMAT_RULES_DOC_PROVIDER.header_label,
    header_content = [
        "# Deprecated Rules and Macros",
        "",
        "The rules and macros described below are used to format, test and ",
        "copy Swift source files.",
    ],
    symbols = _SWIFTFORMAT_RULES_DOC_PROVIDER.symbols,
)

# Write the API headers
[
    write_header(
        name = doc_prov.header_label,
        out = doc_prov.header_basename,
        header_content = [
            "# `{name}` API".format(name = doc_prov.name),
        ],
    )
    for doc_prov in _API_DOC_PROVIDERS
    if doc_prov.is_stardoc
]

# MARK: - Special Case api.md

# Write the api.md_ file as a special case.
write_file_list(
    name = "api_doc",
    out = "api.md_",
    doc_provs = _API_DOC_PROVIDERS,
    header_content = [
        "# API",
        "",
        "The APIs list below are used by rules_swifttidy.",
        "",
    ],
)

# MARK: - Generate Documentation from Providers

doc_for_provs(doc_provs = _ALL_DOC_PROVIDERS)

bzlformat_pkg(name = "bzlformat")
